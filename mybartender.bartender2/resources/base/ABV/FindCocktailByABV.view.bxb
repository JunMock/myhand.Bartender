result-view {
	
	match: CocktailByABV(cocktail_abv) {
		from-output: findABVList
	}
	message {
		if(size(cocktail_abv) == 1){
			template ("[#{value(cocktail_abv.id)}]")
		}
	}
	render {
		
		if (size(cocktail_abv) > 1) {
			list-of (cocktail_abv) {
				has-details (true)
				where-each (item) {
					layout-macro (FindCocktailByABV-Image) {
						param (cocktail_abv) {
							expression (item)
						}
					}
				}
			}
		}
		else-if(size(cocktail_abv) == 1) {
			layout {
				section {
					content {
						layout-macro (FindCocktailByABV-Image) {
							param (cocktail_abv) { expression (cocktail_abv) }
						}
					}
				}
				section {
					title (설명)
					content {
						paragraph {
							value ("[#{value(cocktail_abv.description)}]")
							style (Detail_M)
						}
					}
				}
				if(!cocktail_abv.isbase){
					section {
						title (필수 재료)
						content {
							paragraph {
								value ("[#{value(cocktail_abv.material)}]")
								style (Detail_M)
							}
						}
					}
					section {
						title (부가 재료)
						content {
							paragraph {
								value ("[#{value(cocktail_abv.subMaterial)}]")
								style (Detail_M)
							}
						}
					}
					section{
						content{
							input-cell {
								label {
									template ("#{value(cocktail_abv.name)}")
								}
								value {
									template ("레시피 페이지로 이동하기")
								}
								on-click {
									intent {
										goal: findRecipe
										value : $expr(cocktail_abv.id)
									}
								}
							}
						}
					}
				}
				else{
					section {
						title ("\"#{value(cocktail_abv.name)}\"으로 만들 수 있는 칵테일")
						content {
							for-each (cocktail_abv.recoImage) {
								as (i) {
									thumbnail-card {
										image-position (Start)
										image-url ("[#{value(cocktail_abv.recoImage[i])}]")
										title-area {
											halign (Start)
											slot1 {
												text {
													value ("[#{value(cocktail_abv.recoList[i])}]")
													style (Title_S)
												}
											}
											slot2 {
												paragraph {
													value {
														template ("[#{value(cocktail_abv.recoABV[i])}]")
													}
													style (Detail_L)
												}
											}
											slot3 {
												single-line {
													text {
														value {
															template ("[#{value(cocktail_abv.recoDescription[i])}]")
														}
														style (Detail_M_Soft)
													}
												}
											}
										}
										on-click {
											intent {
												goal: findCocktail
												value-set: SubText{
													$expr(cocktail_abv.recoList[i])
												}
											}
										}
									}
								}
							}
						}
					}
				}
				
			}
		}
	}
	conversation-drivers {
		conversation-driver {
			template ("전체 목록 보여줘")
		}
	}
}